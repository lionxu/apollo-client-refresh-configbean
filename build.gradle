apply from: System.getProperty("user.home") + '/code/publish.gradle'

buildscript {
    ext {
        springBootVersion = '2.0.5.RELEASE'
    }
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

group 'com.github.selfancy'
version '1.0.0-RELEASE'

apply plugin: 'java'
apply plugin: 'signing'
apply plugin: 'maven-publish'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-autoconfigure'
    implementation 'com.ctrip.framework.apollo:apollo-client:1.3.0'
}

bootJar.enabled = false
jar.enabled = true

java {
    withSourcesJar()
    withJavadocJar()
}

javadoc {
    options.addStringOption("charset", "UTF-8")
}

publishing {
    System.setProperty("org.gradle.internal.publish.checksums.insecure", "true")

    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'apollo-client-refresh-configbean'
                description = 'Support for apollo-client to dynamic auto refresh @ConfigurationProperties bean.'
                url = 'https://github.com/selfancy/apollo-client-refresh-configbean'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name = 'Mike Joe'
                        email = 'mail.mikejoe@qq.com'
                    }
                }
                scm {
                    url = 'https://github.com/selfancy/apollo-client-refresh-configbean'
                    connection = 'scm:git:git://github.com/selfancy/apollo-client-refresh-configbean.git'
                    developerConnection = 'scm:git:ssh://git@github.com:selfancy/apollo-client-refresh-configbean.git'
                }
            }
        }
    }
    repositories {
        maven {
            url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username "${sonatypeUsername}"
                password "${sonatypePassword}"
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

task publish(dependsOn: build, overwrite: true)